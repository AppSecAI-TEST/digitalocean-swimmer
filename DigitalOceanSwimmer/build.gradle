import com.android.builder.core.DefaultManifestParser

apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.yassirh.digitalocean"
        minSdkVersion 8
        targetSdkVersion 20
    }

    Properties props = new Properties()
    props.load(new FileInputStream(file(project.property("DigitalOceanSwimmer.properties"))))

    signingConfigs {
        release {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            zipAlign true
            applicationVariants.all { variant ->
                def file = variant.outputFile
                def manifestParser = new DefaultManifestParser()
                variant.outputFile = new File(props['apkOutputDir'], file.name.replace(".apk", "-" + manifestParser.getVersionName(android.sourceSets.main.manifest.srcFile) + ".apk"))
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.loopj.android:android-async-http:1.4.6'
}
